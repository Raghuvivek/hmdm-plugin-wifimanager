/*
 * Headwind MDM: Open Source Android MDM Software
 * https://h-mdm.com
 *
 * WiFi Manager Plugin
 *
 * Copyright (C) 2020 Headwind Solutions LLC
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

apply plugin: 'com.android.application'

android {
    compileSdkVersion 29
    buildToolsVersion "29.0.2"
    defaultConfig {
        applicationId "com.hmdm.wifimanager"
        minSdkVersion 17
        // TargetSdkVersion should be 28, because in API 29 the WiFi management functions are replaced by a stubs
        // See deprecation notice here: https://developer.android.com/reference/android/net/wifi/WifiManager
        targetSdkVersion 28
        versionCode 19
        versionName "1.19"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        debug {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    flavorDimensions "all"

    productFlavors {
        // Main build
        regular {
        }
        // A build using com.hmdm as a shared user ID.
        // This variant works on Android 10+ with Headwind MDM, but requires the same
        // signature as Headwind MDM
        shared {
        }
    }

    applicationVariants.all { variant ->
        if (variant.name.contains("debug")) {
            variant.outputs.all { output ->
                outputFileName = "WiFiManager_v${variant.versionName}(${variant.versionCode}).apk"
            }
        }
    }
    applicationVariants.all { variant ->
        if (variant.name.contains("release")) {
            variant.outputs.all { output ->
                outputFileName = "WiFiManager_v${variant.versionName}(${variant.versionCode})_${getDate()}.apk"
            }
        }
    }
}

def getDate() {
    return new Date().format('dd.MM.yyyy_HH_mm_ss')
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar', '*.aar'])
    implementation 'androidx.appcompat:appcompat:1.1.0'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
    implementation "androidx.recyclerview:recyclerview:1.1.0"
    implementation 'com.jakewharton:butterknife:10.2.1'
    annotationProcessor 'com.jakewharton:butterknife-compiler:10.2.1'
    implementation 'io.reactivex:rxandroid:1.0.1'
    implementation 'io.reactivex:rxjava:1.0.14'
    implementation 'com.squareup.retrofit2:retrofit:2.1.0'
    implementation 'com.squareup.retrofit2:adapter-rxjava:2.1.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.0.2'
    implementation 'com.squareup.okhttp3:logging-interceptor:3.9.1'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'com.yandex.android:mobmetricalib:3.10.0'
}
